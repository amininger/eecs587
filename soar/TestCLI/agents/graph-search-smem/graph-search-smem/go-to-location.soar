sp {graph-search*propose*go-to-location
   (state <s> ^name graph-search
              ^mission.current <next>)
-->
   (<s> ^operator <op> + =)
   (<op> ^name go-to-location
         ^next-location <next>)
}

sp {apply*go-to-location*update-mission
   (state <s> ^operator.name go-to-location
              ^current-location.id <id>
              ^mission <mission>)
   (<mission> ^current <current>)
   (<current> ^next <next> ^id <id>)
-->
   (<mission> ^current <current> -
                       <next>)
}

####### RETRIEVE WAYPOINT DATA
sp {apply*go-to-location*get-waypont*smem*retr
   (state <s> ^operator  <o>
              ^smem.command <cmd>)
   (<o> ^name go-to-location
        ^next-location <next>)
   (<next> -^id <id>)
-->
   (<cmd> ^retrieve <next>)}


sp {apply*go-to-location*get-waypont*smem
   (state <s> ^operator  <o>
              ^smem.command <cmd>)
   (<o> ^name go-to-location
        ^next-location <next>)
   (<next> ^id <id>
          -^x)
-->
   (<cmd> ^query <q>)
   (<q> ^id <id>
        ^type waypoint
        ^x <x>)}

sp {apply*goto-location*retrieve*waypoint
   (state <s>  ^operator  <o>
               ^smem <smem>)
   (<smem> ^command <cmd>
           ^result <res>)
   (<o> ^name go-to-location
        ^next-location <next>)
   (<cmd> ^query <q>)
   (<res> ^retrieved <cp>
          ^success <q>)
   (<q> ^id <id>)
   (<cp> ^x <x>
         ^y <y>
         ^id <id>)
-->
   (write (crlf) |Retrieved | <q> | id | <id> | x | <x> | y | <y> )
   (<next> ^x <x>
           ^y <y>)}