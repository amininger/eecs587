sp {move-block*elaborate*problem-space*top-state
   (state <s> ^superstate.operator.name evaluate-operator
              ^name move-block
              ^superstate.superstate.top-state <ts>)
   (<ts> ^object <tblock1> <tblock2> <tblock3> <table>
         ^gripper <gripper>)
   (<tblock1> ^name A)
   (<tblock2> ^name B)
   (<tblock3> ^name C)
   (<table> ^name table)
-->
   (<s> ^top-state <tsn>)
   (<tsn> ^gripper <ngripper>
          ^object <tblock1> <tblock2> <tblock3> <table>)
}

sp {move-block*elaborate*problem-space*top-state*both
   (state <s> ^superstate.operator.name evaluate-operator
              ^name  move-block
              ^top-state <nts>
              ^superstate.superstate.top-state <ts>)
   (<ts> ^ontop <ontop1>)
   (<ontop1> ^top-block <tblock1>
             ^bottom-block <bblock1>)
-->
   (<nts> ^ontop <nontop3>)
   (<nontop3> ^top-block <tblock1>
              ^bottom-block <bblock1>)
}

sp {move-block*elaborate*problem-space*top-state*one
   (state <s> ^superstate.operator.name evaluate-operator
              ^name move-block
              ^top-state <nts>
              ^superstate.superstate.top-state <ts>)
   (<ts> ^ontop <ontop1>)
   (<ontop1> ^top-block <tblock1>
            -^bottom-block <bblock1>)
-->
   (<nts> ^ontop <nontop3>)
   (<nontop3> ^top-block <tblock1>)
}

sp {move-block*elaborate*problem-space*top-state*gripper*holding
   (state <s> ^superstate.operator.name evaluate-operator
              ^name move-block
              ^superstate.superstate.top-state.gripper <gripper>
              ^top-state.gripper { <> <gripper> <ngripper> })
   (<gripper> ^holding <holding>)
-->
   (<ngripper> ^holding <holding>)}

sp {move-block*elaborate*problem-space*top-state*gripper
   (state <s> ^superstate.operator.name evaluate-operator
              ^name move-block
              ^superstate.superstate.top-state.gripper <gripper>
              ^top-state.gripper { <> <gripper> <ngripper> })
   (<gripper> ^above <above>
              ^open <open>
              ^position <position>)
-->
   (<ngripper> ^above <above>
               ^open <open>
               ^position <position>)}

######################
sp {evaluate-operator*elaborate*state*tried-tied-operator*pickup*putdown*rules
   :default
   (state <s> ^operator <o>
              ^look-ahead-operator <o>)
   (<o> ^name << pick-up put-down >>)
-->
   (<s> ^tried-tied-operator <o>)}
