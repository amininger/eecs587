epmem --set learning on
epmem --set trigger dc
epmem --set phase output

w 0

sp {simple*propose*init-simple
   (state <s> ^superstate nil
             -^name)
-->
   (<s> ^operator <o> +)
   (<o> ^name init-simple)
}

sp {simple*apply*init-simple
   (state <s> ^operator.name init-simple)
-->
   (<s> ^name simple
        ^time 2)
}

# Simple step operator that advances time

sp {propose*step*operator
   (state <s> ^name simple
              ^time <t>)
-->
   (<s> ^operator <o> +)
   (<o> ^name step
        ^time <t>)
   (write (crlf) |Starting step | <t>)
}

sp {apply*step*operator
   (state <s> ^name simple
              ^operator.name step
              ^time <t>)
-->
   (<s> ^time <t> -
        ^time (+ <t> 1))
}


# handles epmem queries
sp {apply*step*remove*queries
   (state <s> ^name simple
              ^operator.name step
              ^epmem <epmem>)
   (<epmem> ^command <cmd>
            ^result <res>)
   (<cmd> ^query <q>)
   (<res> ^{<< success failure >>} <q>)
-->
   (<cmd> ^query <q> -)
}

sp {apply*step*query*failure
   (state <s> ^name simple
              ^operator.name step
              ^epmem.result.failure)
-->
   (write (crlf) |EPMEM QUERY FAILURE|)
}

sp {apply*step*query*success
   (state <s> ^name simple
              ^operator.name step
              ^epmem.result <res>)
   (<res> ^success 
          ^memory-id <id>)
-->
   (write (crlf) |EPMEM QUERY SUCCESS: | <id>)
}

sp {apply*step*query*stats
   (state <s> ^name simple
              ^operator.name step
              ^epmem.result <res>)
   (<res> ^success 
          ^match-cardinality <card>
          ^normalized-match-score <score>
          ^graph-match <graph>)
-->
   (write (crlf) |(card, score, graph): (| <card> |, | <score> |, | <graph> |)|)
}



# Time 2:
# (<s> ^shallow sh
#	   ^deep <d>)
# (<d> ^left d-left)

sp {simple*apply*step2
   (state <s> ^name simple
              ^operator.name step
              ^time 2)
-->
   (<s> ^shallow sh
        ^deep.left d-left)
}

# Time 3:
# (<s> ^deep <d>)
# (<d> ^left d-left
#	   ^right d-right)

sp {simple*apply*step3
   (state <s> ^name simple
              ^operator.name step
              ^time 3
              ^deep <d>)
-->
   (<s> ^shallow sh -)
   (<d> ^right d-right)
}

# Time 4:
# WAIT 1 DC


# Time 5:
# (<s> ^shallow sh
#	   ^deep <d1>
#	   ^deep <d2>)
# (<d1> ^left d-left)
# (<d2> ^right d-right)

sp {simple*apply*step5
   (state <s> ^name simple
              ^operator.name step
              ^time 5
              ^deep <d>)
-->
   (<d> ^right d-right -)
   (<s> ^deep.right d-right
        ^shallow sh) 
}

# Time 6: Clear out memory
sp {simple*apply*step6
   (state <s> ^name simple
              ^operator.name step
              ^time 6
              ^deep <d1>
              ^deep <d2>)
   (<d1> ^right d-right)
   (<d2> ^left d-left)
-->
   (<s> ^deep <d1> -
        ^deep <d2> -
        ^shallow sh -)
}


# Time 7: 
# Simple query
sp {simple*apply*step*7
   (state <s> ^name simple
              ^operator.name step
              ^time 7
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^deep.left d-left)
}


# Time 9: 
# Full surface match
sp {simple*apply*step*9
   (state <s> ^name simple
              ^operator.name step
              ^time 9
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^deep.left d-left
        ^deep.right d-right)
}

# Time 11:
# Complete graph match farther back than a full surface match

sp {simple*apply*step*11
   (state <s> ^name simple
              ^operator.name step
              ^time 11
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^deep <d>)
   (<d> ^left d-left
        ^right d-right)
}

# Time 13:
# No exact match in the past

sp {simple*apply*step*13
   (state <s> ^name simple
              ^operator.name step
              ^time 13
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^deep <d>
        ^shallow sh)
   (<d> ^left d-left
        ^right d-right)
}

# Time 15
# Should fail

sp {simple*apply*step*15
   (state <s> ^name simple
              ^operator.name step
              ^time 15
              ^epmem.command <cmd>)
-->
   (<cmd> ^query <q>)
   (<q> ^deep.middle d-middle)
}


sp {simple*apply*step*20
   (state <s> ^name simple
              ^operator.name step
              ^time 7)#20
-->
   (halt)
}
   
